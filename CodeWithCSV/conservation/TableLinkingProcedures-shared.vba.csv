1	0	Option Compare Database
2	0	
3	0	Option Explicit
4	0	
5	1	'******************************************************************************
6	1	' This module was introduced in version 1 - its checks the table links and
7	1	' deals with logging the user in so Access connects to SQL Server correctly
8	1	'
9	1	' This module is also used in all other interfaces. Only shared improvements
10	1	' should be held here as it will be imported to other mdbs
11	1	'******************************************************************************
12	0	
13	0	
14	0	Function LogUserIn(username As String, pwd As String)
15	1	'******************************************************************************
16	1	' When a user first tries to look at one of the tables SQL Server can throw back
17	1	' the message "Login Failed for user (null). Reason: not associated with a trusted SQL server connection"
18	3	' followed by a login box where the 'Use trusted connection' box must be unchecked before the login details
19	1	' can be entered. This is confusing to users.
20	1	'
21	1	' To overcome this Access will obtain the users login details and by refreshing the link
22	2	' on just one table will enable all table links to work successfully. Its doesn't store the login details
23	1	' so it will always check on start up to facilitate different users.
24	1	'
25	2	' If the user doesn't login successfully the system will quit.
26	1	'
27	1	' SAJ v. 1
28	1	'******************************************************************************
29	0	On Error GoTo err_LogUserIn
30	0	
31	0	Dim retVal
32	0	
33	0	
34	0	If username <> "" And pwd <> "" Then
35	1	    'user login name and password obtained
36	0	    Dim mydb As DAO.Database, I, errmsg, connStr
37	0	    Dim tmptable As TableDef
38	0	    Set mydb = CurrentDb
39	0	    
40	0	    Dim myq As QueryDef
41	0	    Set myq = mydb.CreateQueryDef("")
42	0	    connStr = ""
43	0	    
44	1	    'now find the first linked table in the tables collection (ignoring local tables)
45	1	    'to get the connection string to the sql server DB
46	1	    For I = 0 To mydb.TableDefs.Count - 1 'loop the tables collection
47	0	    
48	0	
49	0	         Set tmptable = mydb.TableDefs(I)
50	0	             
51	0	        If tmptable.Connect <> "" Then
52	1	            'only deal with a table that is linked (not local)
53	1	            'first check if the user login is valid -
54	1	            'this querydef check comes from MSDN KB
55	1	            'it will ensure the user details are valid and return a trappable error. This overcomes
56	1	            'the problem with the refresh link that would make sql server bring up its own login if
57	1	            'the refresh was done with invalid user details. By entering the login into the sql server
58	1	            'login box the connection between what the user had entered into the access form
59	1	            'and what sql server was using would be broken.
60	0	            
61	0	            If connStr = "" Then connStr = tmptable.Connect
62	0	            
63	0	            On Error Resume Next
64	0	                myq.Connect = tmptable.Connect & ";UID=" & username & ";PWD=" & pwd
65	2	                myq.ReturnsRecords = False 'don't waste resources bringing back records
66	4	                myq.sql = "select [ConservationRef_Year] from [Conservation_Basic_Record] WHERE [ConservationRef_Year] = '2000'" 'this is a shared and core table so should always be avail, the record doesn't have to exist
67	0	                myq.Execute
68	0	            
69	1	            If Err <> 0 Then 'the login deails are incorrect
70	0	                GoTo err_LogUserIn
71	0	            Else
72	1	                'reset error trap
73	0	                On Error GoTo err_LogUserIn:
74	1	                ' the login is ok, so now try to refresh the link by adding on the UID and PWD
75	2	                'tmptable.Connect = ";DATABASE=" & g_datapath 'if its were to a file
76	2	                'tmptable.Connect = tmptable.Connect & ";UID=" & username 'this will bring up the SQL server login box for pswd - better than previously as at least ready to recieve it
77	0	                tmptable.Connect = tmptable.Connect & ";UID=" & username & ";PWD=" & pwd
78	0	                tmptable.RefreshLink
79	0	            End If
80	0	            
81	1	            Exit For 'only necessary for one table for Access to set up the correct link to SQL Server
82	0	        End If
83	0	            
84	0	    Next I
85	0	    
86	0	
87	0	Else
88	1	    'somehow got here without username and pwd - catchall
89	0	    MsgBox "Both a username and password are required to operate the system correctly. Please quit and restart the application.", vbCritical, "Login problem encountered"
90	0	End If
91	0	
92	0	
93	1	SetGeneralPermissions username, pwd, connStr 'use this is you want to know what level of priviledge the user has
94	1	'if user enters invalid login sql server will ask for proper one and if its ok connect on that
95	1	'and the link between the sql login and the access one is completely lost -****the querydef conn
96	1	'intro above should mean they always enter valid logon
97	0	LogUserIn = True
98	0	
99	0	
100	0	
101	0	cleanup:
102	0	    myq.Close
103	0	    Set myq = Nothing
104	0	    Set tmptable = Nothing
105	0	    mydb.Close
106	0	    Set mydb = Nothing
107	0	        
108	0	Exit Function
109	0	
110	0	err_LogUserIn:
111	0	
112	0	    
113	0	    If Err.Number = 3059 Or Err.Number = 3151 Then
114	1	        '3059 = operation cancelled by user - probably as login incorrect, sql server has asked for login as well and user pressed CANCEL
115	1	        '3151 = covers all the errors that could happen if user login incorrect, odbc not exist or internet conn not on
116	0	        errmsg = "Sorry but the system cannot log you into the database. There are three reasons this may have occurred:" & Chr(13) & Chr(13)
117	0	        errmsg = errmsg & "1. Your login details have been entered incorrectly" & Chr(13) & Chr(13)
118	0	        errmsg = errmsg & "2. There is no ODBC connection to the database setup on this computer." & Chr(13) & "    See http://www.catalhoyuk.com/database/odbc.html for details." & Chr(13) & Chr(13)
119	0	        errmsg = errmsg & "3. Your computer is not connected to the Internet at this time." & Chr(13) & Chr(13)
120	0	        errmsg = errmsg & "Do you wish to try logging in again?"
121	0	        retVal = MsgBox(errmsg, vbCritical + vbYesNo, "Login Failure")
122	0	        If retVal = vbYes Then
123	1	            GoTo cleanup 'used to be resume before querydef intro, now just cleanup and leave so user can try again
124	0	        Else
125	2	            'user says they dont want to try logging in again, double check in case they didn't understand so tell them we are quitting!
126	0	            retVal = MsgBox("Are you really sure you want to quit and close the system?", vbCritical + vbYesNo, "Confirm System Closure")
127	0	            If retVal = vbNo Then
128	2	                GoTo cleanup 'on 2nd thoughts the user doesn't want to quit so now just cleanup and leave so user can try again
129	0	            Else
130	0	                MsgBox "The system will now quit" & Chr(13) & Chr(13) & "The error reported was: " & Err.Description, vbCritical, "Login Failure"
131	0	            End If
132	0	        End If
133	1	    'ElseIf Err.Number = 3151 Then
134	4	    '    'ODBC--connection to 'xxxxx' failed.
135	2	    '    'odbc name cannot be found on this machine - send off to function to deal with
136	1	    '    AlterODBC
137	0	    Else
138	0	        MsgBox Err.Description & Chr(13) & Chr(13) & "The system will now quit", vbCritical, "Login Failure"
139	0	    End If
140	0	    LogUserIn = False
141	0	    DoCmd.Quit acQuitSaveAll
142	0	End Function
